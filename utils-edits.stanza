defpackage utils/edits :
  import core
  import collections
  import utils/seqable

public var *trace-edits?*:True|False = false
val *edit-ids* = to-seq(0 to false)
public defn next-edit-id () : next(*edit-ids*)

public defstruct Edit :
  name : String
  id : Int with: (init => next-edit-id())
  edit-redo : () -> ?
  edit-undo : () -> ?
  value : ?

public defn Edit (name:String, redo:() -> ?, undo:() -> ?) :
  Edit(name, redo, undo, false)

defmethod print (o:OutputStream, e:Edit) :
  print(o, "Edit(%_, %_, %_)" % [name(e), id(e), value(e)])

public defn redo (e:Edit) : edit-redo(e)()
public defn undo (e:Edit) : edit-undo(e)()

public defn SlotEdit (name:String, object:?, prev:?, next:?, setter:(?, ?) -> False) :
  Edit(name, { setter(object, next) }, { setter(object, prev) }, [ object, next ])
  ;; Edit(name, { (println("REDO %_ O %_ N %_" % [name, object, next]) when name == "QuadData", setter(object, next)) }, 
  ;;            { (println("UNDO %_ O %_ N %_" % [name, object, prev]) when name == "QuadData", setter(object, prev)) }, [ object, next ])
  
public defn Edit (name:String, table:IntTable, key:Int, next:?) :
  if key?(table, key) :
    val prev = table[key]
    Edit(name, { table[key] = next }, { table[key] = prev }, next)
  else :    
    Edit(name, { table[key] = next }, { remove(table, key) }, next)

public defn AddDelEdit (name:String, table:IntTable, key:Int, next:?) :
  Edit(name, { table[key] = next }, { remove(table, key) }, next)

public defn DelAddEdit (name:String, table:IntTable, key:Int, prev:?) :
  Edit(name, { remove(table, key) }, { table[key] = prev }, prev)

public defn AddDelEdit (name:String, table:HashTable, key:?, next:?) :
  Edit(name, { table[key] = next }, { remove(table, key) }, next)

public defn DelAddEdit (name:String, table:HashTable, key:?, prev:?) :
  Edit(name, { remove(table, key) }, { table[key] = prev }, prev)

public defn AddDelEdit (name:String, table:HashSet, key:?) :
  Edit(name, { add(table, key) }, { remove(table, key) })

public defn DelAddEdit (name:String, table:HashSet, key:?) :
  Edit(name, { remove(table, key) }, { add(table, key) })

public defn Edit<?T> (name:String, s:HashSet<?T>, next:Seqable<T>) :
  val prev = to-tuple $ s
  Edit(name, { set-all(s, next) }, { set-all(s, prev) })

public defn Edit<?T> (name:String, v:Vector<?T>, next:Seqable<T>) :
  val prev = to-tuple $ v
  Edit(name, { set-all(v, next) }, { set-all(v, prev) })

public defn AddDelEdit<?T> (name:String, v:Vector<?T&Equalable>, elt:T&Equalable) :
  Edit(name, { add(v, elt) }, { remove-item(v, elt) })
  ;; Edit(name, { (println("ADD %_ ELT %_" % [v, elt]) when name == "ADD-ALL-PATHS", add(v, elt)) }, 
  ;;            { (println("DEL %_ ELT %_" % [v, elt]) when name == "ADD-ALL-PATHS", remove-item(v, elt)) })

public defn DelAddEdit<?T> (name:String, v:Vector<?T&Equalable>, elt:T&Equalable) :
  Edit(name, { remove-item(v, elt) }, { add(v, elt) })

public defstruct Edits :
  index : Int with: (init => 0, setter => set-index)
  marks : Vector<Int> with: (init => Vector<Int>())
  elts : Vector<Edit> with: (init => Vector<Edit>())
with:
  constructor => #Edits

defmethod print (o:OutputStream, e:Edits) :
  print(o, "Edits(%_, %_, %_)" % [index(e), marks(e), length(elts(e))])

public defn Edits () :
  val e = #Edits()
  add(marks(e), 0)
  e

public defn find-next-target (edits:Edits, idx:Int) -> Int :
  let loop (i:Int = 0) :
    if i < length(marks(edits)) :
      if marks(edits)[i] > idx :
        marks(edits)[i]
      else :
        loop(i + 1)
    else :
      length(elts(edits))
           
public defn find-prev-target (edits:Edits, idx:Int) -> [Int, Int] :
  let loop (i:Int = length(marks(edits)) - 1) :
    if i > 0 :
      if marks(edits)[i] < idx :
        [marks(edits)[i], i]
      else :
        loop(i - 1)
    else :
      [0, 0]

public defn undo (edits:Edits) :
  ;; println("UNDOING EDITS %_" % [edits])
  val [target, mark-index] = find-prev-target(edits, index(edits))
  for i in (index(edits) - 1) through target by -1 do :
    ;; println("UNDOING %_ %_" % [i, elts(edits)[i]]) when *trace-edits?*
    undo(elts(edits)[i])
  set-index(edits, target)
  
public defn redo (edits:Edits) :
  ;; println("REDOING EDITS %_" % [edits])
  val next-target = find-next-target(edits, index(edits))
  for i in index(edits) to next-target do :
    redo(elts(edits)[i])
  set-index(edits, next-target)
  
public defn add-mark (edits:Edits) :
  add(marks(edits), index(edits))

public defn clear (edits:Edits) :
  ;; println("CLEARING")
  clear(elts(edits))
  shorten(marks(edits), 1)
  set-index(edits, 0)

defn pop (edits:Edits, idx:Int) :
  val [target, mark-index] = find-prev-target(edits, idx + 1)
  ;; println("POPPING %_ TO TARGET %_ MARK-INDEX %_" % [idx, target, mark-index])
  shorten(marks(edits), mark-index + 1)
  shorten(elts(edits), idx)
  
public defn add (edits:Edits, edit:Edit) :
  pop(edits, index(edits))
  add(elts(edits), edit)
  set-index(edits, length(elts(edits)))
  println("ADD %_ INDEX %_" % [edit, index(edits)]) when *trace-edits?* 

public var *edits*:False|Edits = false

public defn add-edit (e:Edit) :
  add(*edits* as Edits, e) when *edits* is Edits

public defn add-edit (f:() -> Edit) :
  add-edit(f()) when *edits* is Edits

public defn add-do-edit (e:Edit) :
  redo(e)
  add-edit(e)
